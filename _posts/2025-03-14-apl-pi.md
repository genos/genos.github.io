---
title: "APL Pi Day"
layout: post
use_katex: true
---

# Happy \\( \pi \\) Day

In honor of today's [date](https://en.wikipedia.org/wiki/Pi_Day), I played
around with different ways of approximating everyone's favorite constant in
[Dyalog APL](https://www.dyalog.com/).
What follows are translations of a few entries from
[this](https://en.wikipedia.org/wiki/List_of_formulae_involving_%CF%80) fun
Wikipedia list.
I tried to avoid anything that felt like cheating, e.g. using trigonometric
functions or other constants like \\( \phi \\).

First, we set index-origin to its correct value `⎕io←0`.
Next up, here's the familiar "throw darts at the unit square" Monte Carlo
approach (the first line sets our random seed):

```apl
    ⎕rl←1729
    {4×(+⌿÷≢)1>+/×⍨?⍵ 2⍴0}¨10*⍳5
4 3.6 3.12 3.144 3.162
```

Eschewing stochasticity, using Riemann sums for calculating the area of the
unit circle converges faster:

```apl
      6 1⍴{(4÷×⍨⍵)×+⌿((×⍨⍵)-(×⍨1+⍳⍵))*0.5}¨10*⍳6
0
2.904518326
3.120417032
3.139555467
3.141391478
3.141572616
```

as does this translation of the first entry in the iterative algorithm from the
Wikipedia list (as of this writing), viz.

\\[
a_0 = 1, \qquad a_{n + 1} = \left(1 + \frac{1}{2n + 1}\right)a_n, \\quad \pi = \lim_{n\to\infty}\frac{a_n^2}{n}
\\]


```apl
     6 1⍴{(÷⍵)××⍨×⌿{1+÷1+2×⍵}¨⍳⍵}¨10*⍳6
4
3.221088997
3.149456428
3.14237815
3.141671194
3.141600508
```

Leibniz's summation converges at a similar rate:

```apl
      6 1⍴{4×+⌿(¯1∘*÷(1+2∘×))¨⍳⍵}¨10*⍳6
4
3.041839619
3.131592904
3.140592654
3.141492654
3.141582654
```

Though as the
[corresponding](https://en.wikipedia.org/wiki/Leibniz_formula_for_%CF%80#Convergence)
entry notes, this is still sublinear.
The fastest one I tried is a slight reworking of

\\[
    \sum_{k=0}^\infty \frac{(-1)^k}{4}\left(\frac{2}{4k+1}+\frac{2}{4k+2}+\frac{1}{4k+3}\right)
\\]
This is the first entry from the Wikipedia's list of infinite series which "are
efficient for calculating arbitrary binary digits of \\(\pi\\)," and credited
to the book [Pi
Unleashed](https://link.springer.com/book/10.1007/978-3-642-56735-3).

```apl
      15 1⍴{+⌿{((2÷1+4×⍵)+(÷1+2×⍵)+÷3+4×⍵)÷¯4*⍵}¨⍳⍵}¨⍳15
0          
3.333333333
3.114285714
3.146356421
3.140678766
3.141777944
3.141553701
3.141601054
3.141590807
3.141593065
3.141592561
3.141592675
3.141592649
3.141592655
3.141592653
```
This is a better approximation in 15 steps than the others reached in a million!
